group 'com.netflix'
version '1.6.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'jetty'



dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'com.google.code.gson:gson:2.6.2'
    compile project(':hystrix-core')
    compile project(':hystrix-request-servlet')
    compile project(':hystrix-metrics-event-stream')
    compile files('libs/yahooquote.jar')
    compile 'org.glassfish.jersey.core:jersey-client:2.22.2'
    compile 'org.glassfish.jersey.core:jersey-common:2.22.2'
    compile 'org.slf4j:slf4j-log4j12:1.7.19'
    compile 'org.apache.commons:commons-lang3:3.1'
}

configurations {
    all*.exclude group: 'ch.qos.logback', module: 'logback-core'
    all*.exclude module: 'log4j-over-slf4j'
}
task checkSlf4j {
    description 'Ensure only one SFL4j binding is present in the runtime configuration'

    doLast {
        def bindings = []
        configurations.runtime.each {
            zipTree(it).matching { include 'org/slf4j/impl/StaticLoggerBinder.class' }.each { c ->
                bindings << [it.getName(), c]
            }
        }
        if (bindings.size () > 1) {
            throw new GradleException("Multiple SLF4J bindings found: ${bindings*.getAt(0)}")
        }
    }
}

check.dependsOn checkSlf4j

jettyRun {
    httpPort = 9999
}

jettyRunWar{
    contextPath = ''
}
